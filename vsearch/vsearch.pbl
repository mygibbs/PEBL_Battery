##################################################
##  PEBL Visual Search Task
##  Part of the PEBL Test Battery, and the
##  PEBL Cognitive Decathlon
##
## (c) 2010 Shane T Mueller smueller@obereed.net
##
## This software may be used, adapted, altered, or 
## redistributed without consent of the author.
##################################################

define Start(p)
{
  gWin <- MakeWindow("black")
  gSleepEasy <- 1
  gHomeX <- Round(gVideoWidth/2)
  gHomeY <- Round(gVideoHeight/2)

  gDisplayBoxSizeX <- 600  ##400 pixels/side,centered
  gDisplayBoxSizeY <- 400  ##400 pixels/side,centered
  gSubNum <- GetSubNum(gWin)
  gFileOut <- FileOpenAppend("vsearch-"+gsubnum+".txt")
  FilePrint(gFileOut,"sub trial time numtargs size targchar targcol stim resp corr rt1 rt2")

  MessageBox("You are about to take part in a study where you see a screen with many letters on it, and are searching for a particular target.  At the start of each trial, you will be shown the target.  Then, a screen will appear, and you will need to search for the target.  On some trials, the target will be present; on other trials it will be absent.  When you find the target or determine that it is absent, click the mouse button.  Following the mouse click, At this point, the targets will all be replaced by boxes so that you can no longer see what they were.  When this is done, click on the location of the target or the 'None' button.",gWin)

   ##The following specify the conditions of the experiment.
   targs  <- ["O","X"]
   foils <- ["U","D","G","C","Q"]
   foilcolor <- MakeColor("white")
   searchsize <- [10,20,30]
   numtargs <- [0,1,5]
   targcols <- ["white","green"]   
   reps  <- 5
   
   ##These aggregate the conditions into a single design list.
   sizes  <- (DesignFullCounterbalance(numtargs,searchsize))
   bytargs <- DesignFullCounterbalance(targs,targcols)
   sizebycolor <- DesignFullCounterbalance(sizes,bytargs)
   design <- Shuffle(RepeatList(sizebycolor,reps))
  
  ##Move through each trial of the design.
  trial <- 1
  ##loop through each trial.
  loop(i,design)
   {

     time <- GetTime()
     numTargs <- First(First(i))
     num      <- Second(First(i))
     targ     <- First(Second(i))
     targcol  <-  Second(Second(i))

     out <- SearchTrial(targ,foils, MakeColor(targcol),foilcolor,numTargs,num)
     FilePrint_(gFileOut,gSubNum + " " + trial + " " + time + " " + numTargs + " " + num + " " +targ + " " + targcol)
     loop(j,SubList(out,3,Length(out)))
      {
         FilePrint_(gFileOut," " + j )
      }
    FilePrint(gFileOut,"")
    trial <- trial + 1
   }
  MessageBox("Thank you for taking part in the study.",gWin)
}


##  This runs a single search trial. 
##  targchars is a list of 'target' characters
##  foilchars is a list of 'foil' characters.
##  targcol and foilcol specify the colors of these.
##  if absentoption == 1, there will be a 'NONE' button provided

define SearchTrial(targChar, foilChars,targcol,foilcol,numtargs,num)
{

  ShowCursor(0)  #Get rid of the cursor

  ##Some startup stuff
  fontsize <- 30
  targFont <- MakeFont(gPEBLBaseFont,0,fontsize,targCol,MakeColor("black"),1)
  foilFont <- MakeFont(gPEBLBaseFont,0,fontsize,foilCol,MakeColor("black"),1)
  bgcol   <- MakeColor("grey")

  ##num is the total number in the display set.  We need to create a 
  ##list of stimulus characters exactly that long.

  numfoils <- num - numtargs
  foilset <- DesignBalancedSampling(foilChars,numfoils)

 ##make a label for each target.
 i <- 1
 targs <- []
 bgs <- []
 targChars <- Repeat(targChar,numtargs)
 if(numTargs > 0)
  {
   loop(t,targChars)
    {
    targ <- MakeLabel(t,targFont)
    targs <- Append(targs,[targ,1])
    bg   <- Circle(0,0,18,bgcol,0)
    bgs   <- Append(bgs,bg)
    }
  } else {  #If there are no targets, we still need to display the search character
    targ <- MakeLabel(targChar,targFont)
  }

##make label for each foil (and the backgrounds)
 foils <- []
 loop(f,foilset)
  {
    foil <- MakeLabel(f,foilFont)
    bg   <- Circle(0,0,18,bgcol,0)
    foils <- Append(foils,[foil,2])
    bgs   <- Append(bgs,bg)
  }

   num <- Length(targs)+Length(foils)
 
  #Compute the bounds of the display box
   minx <- gHomeX-gDisplayBoxSizeX/2
   maxx <- gHomeX+gDisplayBoxSizeX/2
   miny <- gHomey-gDisplayBoxSizeY/2
   maxy <- gHomey+gDisplayBoxSizeY/2

   ##create the xy points
   pts <- MakePoints(minx,maxx,miny,maxy,40,num)
  
  complex <- Transpose(Append( Merge( Transpose( Merge(targs,foils)), 
                                 Transpose(pts)),bgs))

   ##Add a 'none' button to the top, but hide it.
    none <- EasyLabel("NONE",gHomeX,50,gWin,fontsize)
    noneback <-  Rectangle(gHomeX,50,140,60,MakeColor("grey"),0)
    AddObject(none,gWin)
    AddObject(noneback,gWin)
    Hide(none)
    Hide(noneback)

   ##Show the target (in this case, the last target)
   AddObject(targ,gWin)
   Move(targ,gHomeX,gHomeY)
   label <- EasyLabel("Target:",gHomeX,gHomeY-80,gWin,fontsize)
   Draw()
   Wait(800)
   Draw()
   Hide(targ)
   
  ##Add all the targets/foils to the specified positions
   tmp <- []
   backs <- []
  loop(i,complex)
  {

    obj <- First(i)   
    x <- Third(i)
    y <- Fourth(i)
    back <- Fifth(i)

    AddObject(obj,gWin)    
    AddObject(back,gWin)
    Show(obj)
    Hide(back)
    Move(back,x,y)
    Move(obj,x,y)
    tmp <- Append(tmp,obj)
    backs <- Append(backs,back)
  }
  
  label.y <- gVideoHeight-50
  label.text <- "Click mouse button when search is complete"
  Draw()
  time1 <- GetTime()
  WaitForDownClick()
  time2 <- GetTime()
  ShowCursor(1)
  SetMouseCursorPosition(gHomeX,gHomeY)
  Draw()
  
  label.text <- "Click on circle at location of target or NONE"
  Show(label) 
  Show(none)
  Show(noneback)
  RemoveObjects(tmp,gWin)
  ShowObjects(backs)
  Draw()
  time3 <- GetTime()
  bgs <- Merge([noneback],bgs)
  resp <-  WaitForClickOnTarget(bgs,Sequence(0,num,1))
  time4 <- GetTime()
  RemoveObjects(bgs,gWin)
  RemoveObject(label,gWin)
  RemoveObject(none,gWin)
  RemoveObject(noneback,gWin)
  if(numtargs==0)
  {  
    RemoveObject(targ,gWin)
  }
  Draw()
  
  if(numtargs == 0)
    {
       corr <- (resp == 0)
    } else {
       corr <- resp <= numtargs
    }
   display <- ListToString(Merge(targChars,foilset))

  return [numtargs, num, display, resp, corr, (time2-time1),(time4-time3)]
}

define RemoveObjects(list,win)
{
   loop(i,Flatten(list))
   { 
     RemoveObject(i,win)
   }
}


define ShowObjects(list)
{
   loop(i,Flatten(list))
   { 
     Show(i)
   }
}

define MakePoints(xmin, xmax, ymin, ymax, tol, num)
{

   ##should you watch the points evolve?
   watch <- 0
   

   ##Note that it is possible to arrange the situation 
   ## so that the distribution will never be satisfied.  So, lets
   ## set up a 'limit' at which point we give up and return the best
   ## we have so far.

   limit <- 100
 

  ## First, just initialize num points.
  pts <- []
  i <- 1
  while(i <= num)
   {

     x <- xmin + Random()*(xmax-xmin)
     y <- ymin + Random()*(ymax-ymin)
     pts <- Append(pts, [x,y])
     i <- i + 1
   }

  #Now, check for the minimum distance between two points.
   tries <- 1
   minpair <-  GetMinDist(pts)


   ##Move one point at a time
   while(First(minpair) < tol and tries < limit)
   {
     pt1id <- Nth(minpair,2)
     pt2id <- Nth(minpair,3)

     pt1 <- Nth(pts,pt1id)
     pt2 <- Nth(pts,pt2id)

    #resample the 'bad' points

     ##Half the time, try to move a little bit away from 
     ##the closest. Otherwise, do it randomly.

 
     pt1 <-   [ xmin + Random()*(xmax-xmin),
                  ymin + Random()*(ymax-ymin) ]


     ##Reassemble pts
     tmp <- RemoveSubset(pts,[pt1id])

     ##See if this configuration is an improvement
     testpts <- Merge(tmp,[pt1])
     minpairtest <-  GetMinDist(testpts)

     ##If we've made an improvement, use it, otherwise abandon it.
     if(First(minpairtest) >= First(minpair))
     {
        minpair <- minpairtest
        pts <- testpts
     }
     tries <- tries + 1
  
   }


  
  return Shuffle(pts)
}

define GetMinDist(pts)
{
   len <- Length(pts)
   index1 <- 1
   mindist <- Dist(First(pts),Nth(pts,2))
   id1 <- 1
   id2 <- 2
 
   loop(i,pts)
   {
     pt1 <- i
     index2 <- index1 + 1
 

     if(index2 <= len)
      {
      rest <- SubList(pts,index2, Length(pts))
      loop(j, rest)
       { 
        d <- Dist(i,j)

        if(d < mindist)
        {
          mindist <- d
          id1 <- index1
          id2 <- index2
        }

        index2 <- index2 + 1
      }
      }
     index1  <- index1 + 1
   }

   return [mindist,id1,id2]
}


##  This function takes a list of anything, and a list of indexes
##  specifying the items to extract from the list.  The indexes can
##  be in any order, but the extracted list will be in the order 
##  of the original list.  Items that are not numbers less than or 
##  equal to the length of the list will be ignored.
define RemoveSubset(list, items)
{
   #Check the arguments to insure they are the proper type
    if(not IsList(list))
	{
	    SignalFatalError("First argument of function [RemoveSubset(<list>,<list>)] is not a list")
	}
    if(not IsList(items))
	{
	    SignalFatalError("First argument of function [RemoveSubset(<list>,<list>)] is not a list")
	}


    returnList <- []      #Make an initial blank list to return
    index <- 1            #keep track of the index
    loop(i, list)
    {
	#If the index is in the items list, add it to the return list
	if(not IsMember(index, items))
	{
	    returnList <- Append(returnList, i)
	}
	index <- index + 1
    }

    return returnList
}
